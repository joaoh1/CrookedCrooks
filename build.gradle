plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name

if (System.env.CI == "true") {
	version = "${project.mod_version}-github.${System.env.GITHUB_RUN_NUMBER}"
} else {
	version = project.mod_version
}

group = project.maven_group

repositories {
	maven { url "http://server.bbkr.space:8081/artifactory/libs-release" }
	maven { url "https://jitpack.io" }
	mavenCentral()
}

dependencies {
	//to change the versions, see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//Temporarily adopted while Jitpack is used.
	modRuntime ("com.github.SuperCoder7979:databreaker:f072a81fbd26e4978b687414a21f1ea4a0077c15") {
		exclude module : "fabric-loader"
	}
	//Required for extra crooks.
	modImplementation "io.github.cottonmc:LibCD:${project.libcd_version}"
	include "io.github.cottonmc:LibCD:${project.libcd_version}"
	//Required for extra configurability.
	modImplementation "com.github.joaoh1:MCDict:${project.mcdict_version}"
	include "com.github.joaoh1:MCDict:${project.mcdict_version}"
	//Required for static data.
	modImplementation "io.github.cottonmc:StaticData:${project.staticdata_version}"
	include "io.github.cottonmc:StaticData:${project.staticdata_version}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}